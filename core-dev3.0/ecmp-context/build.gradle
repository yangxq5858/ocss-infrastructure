// java编译的时候缺省状态下会因为中文字符而失败
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

//定义版本号
ext {

}

dependencies {
    compile("com.ecmp:ecmp-util:$util_version")

    compile("org.springframework:spring-context-support:$spring_version")

    compile(
            //json
            "com.fasterxml.jackson.core:jackson-databind:$jackson_version",
            "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jackson_version",
            "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jackson_version",

            "org.apache.cxf:cxf-rt-rs-client:$cxf_version",
            "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:$jackson_version",
            "javax.ws.rs:javax.ws.rs-api:$javax_version",

            "org.bitbucket.b_c:jose4j:0.6.0",

            // zipkin调用链监控
            "io.zipkin.brave:brave:$zipkin_brave_version",
            "io.zipkin.brave:brave-context-slf4j:$zipkin_brave_version",
            "io.zipkin.brave:brave-instrumentation-jaxrs2:$zipkin_brave_version",
            "io.zipkin.reporter2:zipkin-sender-okhttp3:$zipkin_reporter_version"
    )
    //zookepper
    compile("org.apache.curator:curator-recipes:$curator_version") { exclude group: 'log4j' }
    //jwt
    compile("io.jsonwebtoken:jjwt:0.9.0")
}

jar {
    manifest {
        attributes("Manifest-Version": "1.0")
        attributes("Created-By": "Gradle")
        attributes("Implementation-Title": "ECMP-$project.name")
        attributes("Implementation-Version": "$project.version")
        attributes("Build-Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
    }
}

/////////////////////////////////////上传Maven仓库////////////////////////////////////////
//打包源代码
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

//如果希望gradle install，安装到.m2本地仓库，参考下面的内容
install {
    repositories.mavenInstaller {
        pom.version = "$project.version"
        pom.artifactId = "$project.name"
        pom.groupId = "$project.group"
    }
}

//上传到nexus
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: MAVEN_REPO_URL) {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
            pom.version = "$project.version"
            pom.artifactId = "$project.name"
            pom.groupId = "$project.group"
        }
    }
    return 'Success'
}

// gradle upload
